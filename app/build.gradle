plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}

apply plugin: 'realm-android'

def gitSha() {
    def p = 'git rev-parse --short HEAD'.execute([], project.rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        return ""
    }
    return p.text.trim()
}

android {
    def packageName = "eu.darken.bluemusic"

    signingConfigs {
        release {}
    }
    Properties signingProps = new Properties()
    def signingPropFile = new File(System.properties['user.home'], ".appconfig/${packageName}/signing.properties")
    if (signingPropFile.canRead()) signingProps.load(new FileInputStream(signingPropFile))

    String keyStorePath = System.getenv("STORE_PATH") ?: signingProps["release.storePath"]
    File keyStore = keyStorePath ? new File(keyStorePath) : null
    if (keyStore?.canRead()) {
        signingConfigs {
            release {
                storeFile keyStore
                storePassword System.getenv("STORE_PASSWORD") ?: signingProps['release.storePassword']
                keyAlias System.getenv("KEY_ALIAS") ?: signingProps['release.keyAlias']
                keyPassword System.getenv("KEY_PASSWORD") ?: signingProps['release.keyPassword']
            }
        }
    }

    compileSdkVersion buildConfig.compileSdk

    def versionProperties = new Properties()
    versionProperties.load(new FileInputStream(new File("version.properties")))

    def major = versionProperties.getProperty("project.versioning.major").toInteger()
    def minor = versionProperties.getProperty("project.versioning.minor").toInteger()
    def patch = versionProperties.getProperty("project.versioning.patch").toInteger()
    def build = versionProperties.getProperty("project.versioning.build").toInteger()

    def name = "${major}.${minor}.${patch}-rc${build}"
    def code = major * 10000000 + minor * 100000 + patch * 1000 + build * 10

    defaultConfig {
        applicationId "${packageName}"
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk
        versionCode code
        versionName name
    }
    buildTypes {
        def proguardRulesRelease = fileTree(dir: "proguard", include: ["*.pro"]).asList().toArray()
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt')
            proguardFiles proguardRulesRelease
            proguardFiles 'proguard-rules-debug.pro'
            //debuggable false
        }
        beta {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt')
            proguardFiles proguardRulesRelease
            testProguardFiles getDefaultProguardFile('proguard-android.txt')
            testProguardFiles proguardRulesRelease
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt')
            proguardFiles proguardRulesRelease
            testProguardFiles getDefaultProguardFile('proguard-android.txt')
            testProguardFiles proguardRulesRelease
        }
        applicationVariants.all { variant ->
            if (variant.buildType.name != "debug") {
                variant.outputs.each { output ->
                    output.outputFileName = applicationId + "-v" + defaultConfig.versionName + "(" + defaultConfig.versionCode + ")-" + variant.buildType.name.toUpperCase() + "-" + gitSha() + ".apk"
                }
            }
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            outputs.upToDateWhen { false }
            showStandardStreams = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
    buildFeatures {
        viewBinding true
    }
    namespace 'eu.darken.bluemusic'
    lint {
        abortOnError false
        ignore 'CheckResult'
    }
    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }
}

tasks.withType(Test).configureEach {
    systemProperty "robolectric.logging", "stdout"
}


dependencies {
    // Support libs
    implementation "androidx.appcompat:appcompat:1.3.1"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "com.google.android.material:material:1.4.0"
    implementation "androidx.legacy:legacy-preference-v14:1.0.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'

    implementation "androidx.lifecycle:lifecycle-common:2.3.1"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.3.1"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"

    // RX
    implementation "io.reactivex.rxjava3:rxjava:3.1.1"
    implementation "io.reactivex.rxjava3:rxandroid:3.0.0"
    implementation "io.reactivex.rxjava3:rxkotlin:3.0.1"
    implementation 'com.jakewharton.rx3:replaying-share:3.0.0'
    implementation "com.jakewharton.rx3:replaying-share-kotlin:3.0.0"

    //Dagger
    implementation "com.google.dagger:dagger:${buildConfig.deps.dagger}"
    kapt "com.google.dagger:dagger-compiler:${buildConfig.deps.dagger}"
    kaptAndroidTest "com.google.dagger:dagger-compiler:${buildConfig.deps.dagger}"
    androidTestImplementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation "com.google.dagger:dagger-android:${buildConfig.deps.dagger}"
    implementation "com.google.dagger:dagger-android-support:${buildConfig.deps.dagger}"

    //Timber
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // jUnit
    testImplementation 'junit:junit:4.13.2'

    // Mocktio
    androidTestImplementation 'org.mockito:mockito-core:3.9.0'
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito:2.19.0'

    //Intrumentation tests (UI tests)
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0-alpha03'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.5.0-alpha03'

    // IAP
    implementation 'com.android.billingclient:billing:8.0.0'
}